#!/bin/sed -f

#
# from   : ptxdist-1.99.20
# to     : ptxdist-1.99.21
# symbol : PTXCONF_COMPILER_PREFIX_UBOOT -> PTXCONF_COMPILER_PREFIX_BOOTLOADER
# reason : more generic special compiler handling
#
s/^\(\(# \)\?PTXCONF\)_COMPILER_PREFIX_UBOOT\(.*$\)/\1_COMPILER_PREFIX_BOOTLOADER\3/

#
# from   : ptxdist-2010.05.2
# to     : ptxdist-2010.06.0
# symbol : IMAGE_UBI_VOLUME_SIZE -> IMAGE_UBI_ROOT_VOL_SIZE
# symbol : IMAGE_UBIFS_MAX_LEB_COUNT -> IMAGE_UBIFS_ROOT_MAX_LEB_COUNT
# reason : changes in ubi image generation, we are going to have more than one volumes
#
s/^\(\(# \)\?PTXCONF\)_IMAGE_UBI_VOLUME_SIZE\(.*$\)/\1_IMAGE_UBI_ROOT_VOL_SIZE\3/
s/^\(\(# \)\?PTXCONF\)_IMAGE_UBIFS_MAX_LEB_COUNT\(.*$\)/\1_IMAGE_UBIFS_ROOT_MAX_LEB_COUNT\3/

#
# from   : ptxdist-2015.06.0
# to     : ptxdist-2015.07.0
# symbol : IMAGE_IPKG_EXTRA_ARGS -> IMAGE_XPKG_EXTRA_ARGS
# reason : use same symbol for ipkg and opkb build
#
s/^\(\(# \)\?PTXCONF\)_IMAGE_IPKG_EXTRA_ARGS\(.*$\)/\1_IMAGE_XPKG_EXTRA_ARGS\3/

#
# from   : ptxdist-2019.11.0
# to     : ptxdist-2019.12.0
# symbol : PTXCONF_KERNEL_ARCH_STRING -> GENERIC_KERNEL_ARCH in the search path
# reason : PTXCONF_KERNEL_ARCH_STRING was removed
#
/^PTXCONF_DTC_OFTREE_DTS/s/PTXCONF_KERNEL_ARCH_STRING/GENERIC_KERNEL_ARCH/g

#
# from   : ptxdist-2020.09.0
# to     : ptxdist-2020.10.0
# symbol : PTXCONF_TF_A_PLATFORM -> PTXCONF_TF_A_PLATFORMS
# reason : tf-a can be built for multiple platforms now
#
s/^\(\(# \)\?PTXCONF_TF_A_PLATFORM\>\)\(.*$\)/\1S\3/

#
# from   : ptxdist-2020.07.0
# to     : ptxdist-2020.08.0
# symbol : PTXCONF_DTC -> PTXCONF_KERNEL_DTB
# symbol : PTXCONF_DTC_OFTREE_DTS{,_PATH} -> PTXCONF_KERNEL_DTS{,_PATH}
# reason : dtc package removed. Devicetrees are built and installed with the kernel
#
s/^\(\(# \)\?PTXCONF\)_DTC\>\(.*$\)/\1_KERNEL_DTB\3/
s/^\(\(# \)\?PTXCONF\)_DTC_OFTREE\(_DTS\(\|_PATH\)\)\(.*$\)/\1_KERNEL\3\5/

#
# from   : ptxdist-2022.02.0
# to     : ptxdist-2022.03.0
# symbol : PTXCONF_IMAGE_ROOT_CPIO_GZ -> PTXCONF_IMAGE_ROOT_CPIO, PTXCONF_IMAGE_ROOT_CPIO_COMPRESSION_MODE_GZ
# reason : root.cpio generation consolidated. gz generation now dependent on regular cpio generation.
#
s/^\(\(# \)\?PTXCONF_\)IMAGE_ROOT_CPIO_GZ\>\(.*\)$/\1IMAGE_ROOT_CPIO\3\n\1IMAGE_ROOT_CPIO_COMPRESSION_MODE_GZ\3/
